untyped

global function ItemDrops_Init
global function CreateItem
global function RemoveFromTimer
global function RestartTimer
global function ColorToEscapeCode
global function Roguelike_SyncPlayer

table<string, asset> itemModels = {
    common = $"models/weapons/ammoboxes/mags_cluster.mdl", 
    uncommon = $"models/weapons/ammoboxes/backpack_single.mdl",
    legendary = $"models/weapons/ammoboxes/ammobox_01.mdl",
    umbral = $"models/weapons/ammoboxes/ammobox_bigbullet.mdl",
    titan = $"models/weapons/ammoboxes/ammobox_bigclip.mdl",
    //$"models/weapons/ammoboxes/ammobox_missle.mdl"
}

int itemRollsLost = 0

void function RestartTimer()
{
    SetGlobalNetTime( "difficultyStartTime", Time() - GetConVarInt( "roguelike_time") )
    ForceDifficultyCallbacks()
}

void function RemoveFromTimer( float amount )
{
    SetGlobalNetTime( "difficultyStartTime", GetGlobalNetTime( "difficultyStartTime" ) + amount )
    ForceDifficultyCallbacks()
}
void function AddWeaponCallback()
{
}
void function ItemDrops_Init()
{
    if (IsLobby()) return
	AddCallback_OnNPCKilled(  OnNPCKilled_DropItem )
    AddSpawnCallback( "player", PlayerConnected )
    AddCallback_OnPlayerRespawned( Roguelike_SyncPlayer )
    AddCallback_OnRoguelikeInventoryChanged( void function( entity player, string item, int old, int new ) : ()
    {
        foreach ( p in GetPlayerArray() )
        {
            Remote_CallFunction_NonReplay( p, "ServerCallback_SetItemAmount", p.GetEncodedEHandle(), Roguelike_GetItemNumericId( item ), new )
        }
    } )
    
    AddClientCommandCallback( "give_item", GiveItem )
    AddClientCommandCallback( "all_items", AllItems )
    #if SP
    AddCallback_OnLevelEnd( OnLevelEnd )
    #endif
}

bool function AllItems( entity player, array<string> args )
{
    if (GetConVarInt("sv_cheats") != 1)
        return false

    try
    {
        foreach (string item in Roguelike_GetAllItems())
        {
            Roguelike_GiveEntityItem( player, item, 1 )

            string rarity = Roguelike_GetItemRarity( item )
            Chat_Impersonate( player, format(
                "\x1b[38;2;150;150;150mUsed cheats to obtain %i %s%s\x1b[38;2;150;150;150m (%i)", 
                1, ColorToEscapeCode(Roguelike_GetRarityChatColor( rarity )), Roguelike_GetItemName( item ), Roguelike_GetItemCount(player, item)), false )
        }
    }
    catch (ex)
    {
        printt(ex)   
        //CodeWarning("Unknown Item.")
        //return false
    }
    return true
}

bool function GiveItem( entity player, array<string> args )
{
    if (GetConVarInt("sv_cheats") != 1)
        return false

    if (args.len() < 1)
        return false

    try
    {
        int count = 1
        string item = args[0]
        if (args.len() > 1)
            count = int(args[1])
        Roguelike_GiveEntityItem( player, item, count )

        string rarity = Roguelike_GetItemRarity( args[0] )
        Chat_Impersonate( player, format(
            "\x1b[38;2;150;150;150mUsed cheats to obtain %i %s%s\x1b[38;2;150;150;150m (%i)", 
            count, ColorToEscapeCode(Roguelike_GetRarityChatColor( rarity )), Roguelike_GetItemName( item ), Roguelike_GetItemCount(player, item)), false )
    }
    catch (ex)
    {
        printt(ex)   
        //CodeWarning("Unknown Item.")
        //return false
    }
    return true
}

void function CheckLoan( entity player )
{
    int loanStacks = expect int( player.s.hasLoan )
    int loanPaybackNeeded = loanStacks * GetChestCost() * 8

    if (GetMoney( player ) < loanPaybackNeeded)
        Roguelike_GiveEntityItem( player, "fatigue" )
}

void function OnLevelEnd( string mapName, LevelTransitionStruct trans )
{
    foreach ( index, player in GetPlayerArray())
    {
        if (index > 3) break
        if (player.s.hasLoan)
            CheckLoan(player)
        AddXP( GetMoney( player ) )
        RemoveMoney( player, GetMoney( player ) )
        SetConVarString("player_" + (index + 1) + "_name", player.GetPlayerName() )
        SetConVarString("player_" + (player.GetPlayerIndex() + 1) + "_items", GetItemConVarValue( player ) )
    }
    SetConVarInt( "level_count", levelCount + 1 )
    SetConVarInt( "roguelike_time", int( Time() - GetGlobalNetTime("difficultyStartTime") ) )
}

void function PlayerConnected( entity player )
{
    for (int i = 0; i < 4; i++)
    {
        if (GetConVarString("player_" + (i + 1) + "_name") == player.GetPlayerName() )
            { SetItemsForPlayerFromString(player, GetConVarString("player_" + (i + 1) + "_items")); break; }
    }
    for (int i = 0; i < 4; i++)
    {
        if (GetConVarString("player_" + (i + 1) + "_name") == "")
        {
            SetConVarString("player_" + (i + 1) + "_name", player.GetPlayerName() )
            //SetItemsForPlayerFromString(player, "")
            break
        }
    }

    player.s.hasLoan <- 0
    if (Roguelike_GetItemCount( player, "loan") > 0)
    {
        player.s.hasLoan <- Roguelike_GetItemCount( player, "loan" )
        AddMoney( player, Roguelike_GetItemCount( player, "loan" ) * GetChestCost() * 5 )
    }
    if (Roguelike_GetItemCount( player, "fatigue" ) > 0)
    {
        string fatigueStr = "fatigued..."
        switch (Roguelike_GetItemCount(player, "fatigue"))
        {
            case 2:
                fatigueStr = "VERY fatigued..."
                break
            case 3:
                fatigueStr = "EXTREMELY fatigued..."
                break
            case 4:
                fatigueStr = "wants to lay down a little..."
                break
            case 5:
                fatigueStr = "died from lack of blood."
        }
        Chat_Impersonate( player, format(
            "\x1b[38;2;150;150;150mis %s%s\x1b[38;2;150;150;150m", ColorToEscapeCode(Roguelike_GetRarityChatColor(RARITY_LEGENDARY)), fatigueStr), false )
    }
    float startTime = Time()
    //if (GetMapName() == "sp_crashsite")
    //    startTime += 30 // no longer needed since we pause timer when player is in FirstPersonSequence
    if (player.GetPlayerIndex() == 0)
    {
        int startPointMax = 0
        switch (GetMapName())
        {
            case "sp_s2s":
                startPointMax = 7
                break
            case "sp_hub_timeshift":
                startPointMax = 3
                break
        } 
        if (Roguelike_GetStartPoint() >= startPointMax)
            SetGlobalNetTime("difficultyStartTime", startTime - GetConVarInt("roguelike_time"))
    }

    //Roguelike_SyncPlayer( player )
}

void function Roguelike_SyncPlayer( entity player )
{
    //print("Pain")
    //print(TraceLine(player.GetOrigin(), player.GetOrigin() + <0,0, -1> * 500, [], TRACE_MASK_SOLID, TRACE_COLLISION_GROUP_NONE ).endPos)
    foreach ( item in Roguelike_GetPlayerItems( player ) )
    {
        if (expect int( player.s.hasLoan ) > 0)
            Remote_CallFunction_NonReplay( player, "ServerCallback_SetLoanAmount", player.s.hasLoan * GetChestCost() * 8 )
        //print(item)
        foreach ( p in GetPlayerArray() )
        {
            Remote_CallFunction_NonReplay( p, "ServerCallback_SetItemAmount", p.GetEncodedEHandle(), Roguelike_GetItemNumericId( item ), Roguelike_GetItemCount( player, item ) )
        }
    }
    SetConVarString("player_" + (player.GetPlayerIndex() + 1) + "_items", GetItemConVarValue( player ) )
}

void function OnNPCKilled_DropItem( entity npc, entity attacker, var damageInfo )
{
    /*if (!attacker.IsPlayer()) return
    print("roling for item\nCHANCE: " + (5 + 2 * itemRollsLost) + "%")
    if (RandomFloat(100.0) < 5 + 2 * itemRollsLost)
    {
        string item = Roguelike_GetRandomItem()
        CreateItem( item, npc.GetOrigin(), npc.GetAngles() )
        itemRollsLost = 0
    }
    else itemRollsLost++*/

    if (attacker.IsTitan())
        return
    
    printt("Healing", attacker, "for", Roguelike_GetItemCount( attacker, "heal_on_kill") * 5 + "%", "(",     
    20 * Roguelike_GetItemCount( attacker, "heal_on_kill"), attacker.GetHealth() + 20 * Roguelike_GetItemCount( attacker, "heal_on_kill"), ")")
    if ( IsAlive( attacker) && Roguelike_GetItemCount( attacker, "heal_on_kill") > 0)
        attacker.SetHealth( minint( attacker.GetMaxHealth(), attacker.GetHealth() + 20 * Roguelike_GetItemCount( attacker, "heal_on_kill") ) )
}

entity function CreateItem( string item, vector origin, vector angles )
{
    entity prop_dynamic = CreateEntity( "prop_dynamic" )
    string rarity = Roguelike_GetItemRarity( item )
    asset model = $"models/weapons/ammoboxes/mags_cluster.mdl"//itemModels[rarity]
    origin += <RandomFloatRange( -2, 2 ), RandomFloatRange( -2, 2 ), 0>
    angles += <0, RandomFloatRange( -15, 15 ), 0>
	prop_dynamic.SetValueForModelKey( model )
	prop_dynamic.kv.fadedist = 2000
	prop_dynamic.kv.renderamt = 255
	prop_dynamic.kv.rendercolor = "255 255 255"
	prop_dynamic.kv.solid = 6

    prop_dynamic.SetOrigin( origin )
    prop_dynamic.SetAngles( angles )

    DispatchSpawn(prop_dynamic)

    prop_dynamic.SetOrigin( origin )
    prop_dynamic.SetAngles( angles )

    prop_dynamic.SetScriptName( "item_drop_" + item )

	prop_dynamic.SetUsable()
	//prop_dynamic.SetUsableByGroup( "pilot" )
    //prop_dynamic.SetUsePrompts( "Hold %use% to pick up " + Roguelike_GetItemName( item ), "Press %use% to pick up " + Roguelike_GetItemName( item ) )

    AddCallback_OnUseEntity( prop_dynamic, GrabItemCallback( item ) )
    foreach (entity player in GetPlayerArray())
    {
        Roguelike_SyncPlayer( player )
    }

    SetOutline( prop_dynamic, Roguelike_GetRarityColor( rarity ), 6.5 )

    // doesn't cause pop-up to be removed, also can be fustrating
    //thread DestroyAfterDelay(prop_dynamic, 30)

    return prop_dynamic
}

void function SetOutline( entity ent, vector color, float radius )
{
    Highlight_SetNeutralHighlight( ent, "roguelike_item" )
    ent.Highlight_SetParam( 0, 0, color )
    //ent.Highlight_SetNearFadeDist(700)
    //ent.Highlight_SetFarFadeDist(1000)
	//ent.Highlight_SetFlag( HIGHLIGHT_FLAG_ADS_FADE, highlight.adsFade )
}

function GrabItemCallback( string item ) 
{
    return function( itemObj, player ) : ( item ) {
        expect entity( player )
        expect entity( itemObj )
        Roguelike_GiveEntityItem( player, item )
        Roguelike_UnlockAchievement( player, "pickup" )
        string rarity = Roguelike_GetItemRarity( item )
        if (rarity == RARITY_LEGENDARY)
            Roguelike_UnlockAchievement( player, "pickup_legendary" )
        Chat_Impersonate( player, format(
            "\x1b[38;2;150;150;150mPicked up %s%s\x1b[38;2;150;150;150m (%i)", 
            ColorToEscapeCode(Roguelike_GetRarityChatColor( rarity )), Roguelike_GetItemName( item ), Roguelike_GetItemCount(player, item)), false )
        //Roguelike_SyncPlayer( player )

        itemObj.Destroy()
    }
}

string function ColorToEscapeCode( vector color )
{
    return "\x1b[38;2;" + int(color.x * 255) + ";" + int(color.y * 255) + ";" + int(color.z * 255) + "m"
}

string function GetItemConVarValue( entity player )
{
    string result = ""
    int len = Roguelike_GetPlayerItems(player).len()
    foreach ( index, item in Roguelike_GetPlayerItems(player))
    {
        int itemId = Roguelike_GetItemNumericId( item )
        int amount = Roguelike_GetItemCount( player, item )
        result += itemId + " " + amount
        if (index < len - 1) result += " "
    }
    return result
}

void function SetItemsForPlayerFromString(entity player, string str)
{
    array<string> items = split( str, " ")
    for ( int i = 0; i < items.len(); i += 2 )
    {
        string item = Roguelike_GetItemFromNumericId( items[i].tointeger() )
        int amount = items[i + 1].tointeger()
        Roguelike_GiveEntityItem( player, item, amount )
    }
}  